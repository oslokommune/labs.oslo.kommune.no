plugins {
    id 'java'
    id 'com.github.node-gradle.node' version '5.0.0'
    id 'com.enonic.xp.app' version '3.4.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
}

app {
    name = "${appName}"
    displayName = "${appDisplayName}"
    vendorName = "${vendorName}"
    vendorUrl = "${vendorUrl}"
    systemVersion = "${xpVersion}"
    version = "${version}.${grgit.head().abbreviatedId}"
}

node {
    version = '20.11.1'
    download = true
}

dependencies {
    compileOnly "com.enonic.xp:core-api:${xpVersion}"
    compileOnly "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.0.1"
    include 'com.enonic.lib:lib-cache:2.2.0'
    webjar 'org.webjars.bower:moment:2.24.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
}

task webpackProd(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run', 'build']
}

task webpackDev(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run', 'dev']
}

if (project.hasProperty( 'dev' )) {
    jar.dependsOn += webpackDev
} else {
    jar.dependsOn += webpackProd
}